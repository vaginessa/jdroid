apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.gradle.plugin-publish"

ext.projectName = 'Jdroid Gradle Plugin'
description = 'Gradle Plugin for Java & Android projects'
ext.packaging = 'jar'

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
	}
}

repositories {
	jcenter()
}

dependencies {
    compile localGroovy()

	// Check the release notes at http://tools.android.com/tech-docs/new-build-system
	// https://developer.android.com/tools/revisions/gradle-plugin.html
	// https://jcenter.bintray.com/com/android/tools/build/gradle/
 	compile 'com.android.tools.build:gradle:2.0.0-beta2'


	// https://github.com/JakeWharton/sdk-manager-plugin
	// compile 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'

	compile 'com.google.api-client:google-api-client:1.19.0'
	compile 'com.google.apis:google-api-services-androidpublisher:v2-rev15-1.19.1'

	// Module dependencies
	compile project(':jdroid-java')
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

// Configuration to publish your plugin to the plugin portal
pluginBundle {
	website = 'http://www.jdroidframework.com/'
	vcsUrl = 'https://github.com/maxirosson/jdroid'

	plugins {
		javaPlugin {
			id = 'com.jdroid.java'
			displayName = 'Jdroid Java Gradle Plugin'
			description = 'Gradle Plugin for Java libraries'
			tags = ['jdroid', 'java']
		}
		javaWebPlugin {
			id = 'com.jdroid.javaweb'
			displayName = 'Jdroid Java Gradle Plugin'
			description = 'Gradle Plugin for Java Web apps'
			tags = ['jdroid', 'java']
		}
		androidAppPlugin {
			id = 'com.jdroid.android.application'
			displayName = 'Jdroid Android App Gradle Plugin'
			description = 'Gradle Plugin for Android apps'
			tags = ['jdroid', 'java', 'android']
		}
		androidLibPlugin {
			id = 'com.jdroid.android.library'
			displayName = 'Jdroid Android Library Gradle Plugin'
			description = 'Gradle Plugin for Android libraries'
			tags = ['jdroid', 'java', 'android']
		}
	}

	mavenCoordinates {
		groupId = "com.jdroidframework"
		artifactId = "jdroid-gradle-plugin"
	}
}