apply plugin: 'com.jdroid.android.library'

ext.projectName = 'Jdroid Android - Google Maps'
description = 'Jdroid dependency project for Google Maps'

buildscript {

	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.jdroidframework:jdroid-gradle-plugin:' + project.version
	}
}

repositories {
	// TODO See how to remove this dependency
	maven {
		url 'https://maven.fabric.io/public'
	}
}

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

	compile project(':jdroid-android')

	compile 'com.google.android.gms:play-services-maps:' + googlePlayServicesVersion

	testCompile 'junit:junit:4.12'
	testCompile "org.mockito:mockito-all:1.10.19"

	// https://github.com/robolectric/robolectric
	testCompile('org.robolectric:robolectric:2.4') {
		exclude module: 'classworlds'
		exclude module: 'commons-logging'
		exclude module: 'httpclient'
		exclude module: 'maven-artifact'
		exclude module: 'maven-artifact-manager'
		exclude module: 'maven-error-diagnostics'
		exclude module: 'maven-model'
		exclude module: 'maven-project'
		exclude module: 'maven-settings'
		exclude module: 'plexus-container-default'
		exclude module: 'plexus-interpolation'
		exclude module: 'plexus-utils'
		exclude module: 'wagon-file'
		exclude module: 'wagon-http-lightweight'
		exclude module: 'wagon-provider-api'
	}
}

jdroid {
	notDefaultLanguages = ['es']
}

android {

	lintOptions {
		disable 'ContentDescription', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry', 'UseCompoundDrawables', 'UnknownIdInLayout'
	}

	// TODO We will publish the debug version until we discover how to publish both debug and release
	//publishNonDefault true
	defaultPublishConfig "debug"

	sourceSets.test.java.srcDirs += "build/generated/source/r/debug"
	sourceSets.test.java.srcDirs += sourceSets.main.java.srcDirs
	sourceSets.test.java.srcDirs += sourceSets.debug.java.srcDirs
}

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.sourceFiles, android.sourceSets.debug.java.sourceFiles
}

artifacts {
	archives androidSourcesJar
}
