apply plugin: 'com.jdroid.android.library'

ext.PROJECT_NAME = 'Jdroid Android'
description = 'Jdroid dependency project for Android apps'

configurations {
	compile.exclude group: "org.apache.httpcomponents",  module: "httpclient"
	compile.exclude group: "org.slf4j",  module: "slf4j-api"
}

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

buildscript {
	dependencies {
		classpath 'com.jdroidframework:jdroid-gradle-android-plugin:' + JDROID_GRADLE_PLUGIN_VERSION
	}
}

dependencies {

	compile 'org.slf4j:slf4j-android:1.6.1-RC1'

	compile 'com.android.support:support-v4:' + ANDROID_SUPPORT_VERSION
	compile 'com.android.support:support-v13:'  + ANDROID_SUPPORT_VERSION
	compile 'com.android.support:design:' + ANDROID_SUPPORT_VERSION
	compile 'com.android.support:recyclerview-v7:' + ANDROID_SUPPORT_VERSION
	compile 'com.android.support:appcompat-v7:' + ANDROID_SUPPORT_VERSION
	compile 'com.android.support:cardview-v7:' + ANDROID_SUPPORT_VERSION

	compile 'com.google.android.gms:play-services-analytics:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.android.gms:play-services-base:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.android.gms:play-services-gcm:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.android.gms:play-services-location:' + GOOGLE_PLAY_SERVICES_VERSION

	compile 'com.google.firebase:firebase-core:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.firebase:firebase-appindexing:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.firebase:firebase-messaging:' + GOOGLE_PLAY_SERVICES_VERSION
	compile 'com.google.firebase:firebase-invites:' + GOOGLE_PLAY_SERVICES_VERSION

	debugCompile ('com.squareup.leakcanary:leakcanary-android:' + LEAK_CANARY_VERSION) { force = true }
	releaseCompile ('com.squareup.leakcanary:leakcanary-android-no-op:' + LEAK_CANARY_VERSION) { force = true }
	testCompile ('com.squareup.leakcanary:leakcanary-android-no-op:' + LEAK_CANARY_VERSION) { force = true }

	// https://github.com/facebook/device-year-class
	compile 'com.facebook.device.yearclass:yearclass:1.0.1'

	// Jdroid dependencies

	if (jdroid.getBooleanProp("JDROID_JAVA_AS_PROJECT_DEPENDENCY", false)) {
		compile project(":jdroid-java-core")
	} else {
		compile 'com.jdroidframework:jdroid-java-core:' + JDROID_JAVA_VERSION
	}

	if (jdroid.getBooleanProp("JDROID_JAVA_HTTP_AS_PROJECT_DEPENDENCY", false)) {
		compile project(":jdroid-java-http-core")
	} else {
		compile ('com.jdroidframework:jdroid-java-http-core:' + JDROID_JAVA_HTTP_VERSION) {
			exclude module: 'jdroid-java-core'
		}
	}

	testCompile 'junit:junit:' + JUNIT_VERSION
	testCompile 'org.robolectric:robolectric:' + ROBOLECTRIC_VERSION
}

jdroid {
	notDefaultLanguages = ['es']
}

android {
	sourceSets.test.java.srcDirs += "build/generated/source/r/debug"
	sourceSets.test.java.srcDirs += sourceSets.main.java.srcDirs
	sourceSets.test.java.srcDirs += sourceSets.debug.java.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.debug.resources.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.main.res.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.debug.res.srcDirs
}
